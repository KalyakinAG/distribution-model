
// Вывести заголовок.
// 
// Параметры:
//  ТабличныйДокумент - ТабличныйДокумент - Табличный документ
//  Заголовок - Строка - Заголовок
&НаСервере
Процедура ВывестиЗаголовок(ТабличныйДокумент, Заголовок)
	Секция = ТабличныйДокумент.ПолучитьОбласть("R1");
	Секция.Область("R1C1").Текст = Заголовок;
	ТабличныйДокумент.Вывести(Секция);
КонецПроцедуры

&НаСервере
Процедура ВывестиТаблицу(ТабличныйДокумент, Таблица, Заголовок)
	ВывестиЗаголовок(ТабличныйДокумент, Заголовок);	
	Построитель = Новый ПостроительОтчета;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(Таблица);       
	Построитель.Вывести(ТабличныйДокумент);
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокиИзМакета(Таблица, Макет)
	Строки = ОбщийКлиентСервер.JSONВОбъект(Макет.ПолучитьТекст());
	Для Каждого СтрокаТаблицы Из Строки Цикл
		ЗаполнитьЗначенияСвойств(Таблица.Добавить(), СтрокаТаблицы);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	_Обработка = РеквизитФормыВЗначение("Объект");
	ДобавитьСтрокиИзМакета(Объект.Товары, _Обработка.ПолучитьМакет("Товары"));
	ДобавитьСтрокиИзМакета(Объект.Партии, _Обработка.ПолучитьМакет("Партии"));
	ДобавитьСтрокиИзМакета(Объект.Лимиты, _Обработка.ПолучитьМакет("Лимиты"));
	ДобавитьСтрокиИзМакета(Объект.ПриоритетыЛимитов, _Обработка.ПолучитьМакет("ПриоритетыЛимитов"));
	ДобавитьСтрокиИзМакета(Объект.Авансы, _Обработка.ПолучитьМакет("Авансы"));	
КонецПроцедуры

&НаСервере
Функция РаспределитьСписаниеЛимитов(Заголовок)
	МодельЗапроса = Общий.МодельЗапроса()
	;
	МодельЗапроса.ЗапросПакета().Поместить("ВТ_ТОВАРЫ")
		.Источник(Объект.Товары)
		.Поле("*")
	;
	МодельЗапроса.ЗапросПакета().Поместить("ВТ_ЛИМИТЫ")
		.Источник(Объект.Лимиты)
		.Поле("*")
	;
	МодельЗапроса.ЗапросПакета().Поместить("ВТ_ПРИОРИТЕТЫ_ЛИМИТОВ")
		.Источник(Объект.ПриоритетыЛимитов)
		.Поле("*")
	;
	МодельЗапроса.ВыполнитьЗапрос();
	
	МодельРаспределения = Общий.МодельРаспределения(МодельЗапроса.МенеджерВременныхТаблиц)
	;
	МодельРаспределения
		.Таблица("ВТ_ТОВАРЫ")
			.Измерения()
				.Поле("Товар", "ТоварИсходный")
				.Поле("Статья", "СтатьяИсходная")
			.Реквизиты()
				.Поле("*")
			.Ресурсы()
				.Поле("Сумма")
			.Отбор("НЕ Корректировка")
		.Отношение("ВТ_ПРИОРИТЕТЫ_ЛИМИТОВ")
			.ИзмеренияТаблицы()
				.Поле("*")
			.ИзмеренияБазы()
				.Поле("Товар")
				.Поле("Статья")
			.Порядок("Приоритет")
		.База("ВТ_ЛИМИТЫ")
			.Измерения()
				.Поле("*")
			.Реквизиты()
				.Поле("*")
			.Ресурсы()
				.Поле("Сумма")
	;
	МодельРаспределения.Распределить();

	Результат = Новый ТабличныйДокумент();
	
	ВывестиЗаголовок(Результат, Заголовок);
	//  Исходные таблицы
	ВывестиТаблицу(Результат, Объект.Товары.Выгрузить(), "Товары");
	ВывестиТаблицу(Результат, Объект.Лимиты.Выгрузить(), "Лимиты");
	ВывестиТаблицу(Результат, Объект.ПриоритетыЛимитов.Выгрузить(), "ПриоритетыЛимитов");
	//  Таблицы результата
	ВывестиТаблицу(Результат, МодельРаспределения.РезультатРаспределения, "Распределение по лимитам");
	ВывестиТаблицу(Результат, МодельРаспределения.БазаРаспределения, "Остатки лимитов");	
	ВывестиТаблицу(Результат, МодельРаспределения.ТаблицаРаспределения, "Остатки товаров");					
	
	Возврат Результат;
КонецФункции

&НаСервере
Функция РаспределитьТоварыПоПартиям(Заголовок)
	МодельЗапроса = Общий.МодельЗапроса()
	;
	МодельЗапроса.ЗапросПакета().Поместить("ВТ_ТОВАРЫ")
		.Источник(Объект.Товары)
		.Поле("*")
	;
	МодельЗапроса.ЗапросПакета().Поместить("ВТ_ПАРТИИ")
		.Источник(Объект.Партии)
		.Поле("*")
	;
	МодельЗапроса.ВыполнитьЗапрос();
	
	МодельРаспределения = Общий.МодельРаспределения(МодельЗапроса.МенеджерВременныхТаблиц)
	;
	МодельРаспределения
		.Таблица("ВТ_ТОВАРЫ")
			.Измерения()
				.Поле("Товар")
			.Реквизиты()
				.Поле("*")
			.Ресурсы()
				.Поле("Количество")
			.Отбор("НЕ Корректировка")
		.База("ВТ_ПАРТИИ")
			.Измерения()
				.Поле("*")
			.Реквизиты()
				.Поле("*")
			.Ресурсы()
				.Поле("Количество")
			.Порядок("ДатаПартии")
	;
	МодельРаспределения.Распределить();
	
	Результат = Новый ТабличныйДокумент();
	ВывестиЗаголовок(Результат, Заголовок);
	ВывестиТаблицу(Результат, Объект.Товары.Выгрузить(), "Товары");
	ВывестиТаблицу(Результат, Объект.Партии.Выгрузить(), "Партии");
	ВывестиТаблицу(Результат, МодельРаспределения.РезультатРаспределения, "Распределение по партиям");
	ВывестиТаблицу(Результат, МодельРаспределения.БазаРаспределения, "Остатки по партиям");	
	ВывестиТаблицу(Результат, МодельРаспределения.ТаблицаРаспределения, "Остатки товаров");
	Возврат Результат;
КонецФункции

&НаСервере
Функция РаспределитьЗачет(Заголовок)
	МодельЗапроса = Общий.МодельЗапроса()
	;
	МодельЗапроса.ЗапросПакета().Поместить("ВТ_ТОВАРЫ")
		.Источник(Объект.Товары)
		.Поле("*")
	;
	МодельЗапроса.ЗапросПакета().Поместить("ВТ_АВАНСЫ")
		.Источник(Объект.Авансы)
		.Поле("*")
	;
	МодельЗапроса.ВыполнитьЗапрос();
	
	МодельРаспределения = Общий.МодельРаспределения(МодельЗапроса.МенеджерВременныхТаблиц)
	;
	МодельРаспределения
		.Таблица("ВТ_ТОВАРЫ")
			.Реквизиты()
				.Поле("*")
			.Ресурсы()
				.Поле("Сумма")
				.Поле("СуммаБезНДС")
				.Поле("СуммаНДС")
			.Отбор("НЕ Корректировка")
		.База("ВТ_АВАНСЫ")
			.Реквизиты()
				.Поле("*")
			.Ресурсы()
				.Поле("Сумма")
			.Порядок("ДатаАванса")
	;
	МодельРаспределения.Распределить();
	
	Результат = Новый ТабличныйДокумент();
	ВывестиЗаголовок(Результат, Заголовок);
	ВывестиТаблицу(Результат, Объект.Товары.Выгрузить(), "Товары");
	ВывестиТаблицу(Результат, Объект.Авансы.Выгрузить(), "Авансы");
	ВывестиТаблицу(Результат, МодельРаспределения.РезультатРаспределения, "Распределение авансов по товарам");
	ВывестиТаблицу(Результат, МодельРаспределения.БазаРаспределения, "Остатки авансов");	
	ВывестиТаблицу(Результат, МодельРаспределения.ТаблицаРаспределения, "Остатки товаров");
	Возврат Результат;
КонецФункции

&НаСервере
Функция РаспределитьРекласс(Заголовок)
	МодельЗапроса = Общий.МодельЗапроса()
	;
	МодельЗапроса.ЗапросПакета().Поместить("ВТ_ТОВАРЫ")
		.Источник(Объект.Товары)
		.Поле("*")
	;
	МодельЗапроса.ВыполнитьЗапрос();
	
	МодельРаспределения = Общий.МодельРаспределения(МодельЗапроса.МенеджерВременныхТаблиц)
	;
	МодельРаспределения
		.Таблица("ВТ_ТОВАРЫ")
			.Реквизиты()
				.Поле("НомерСтроки", "НомерСтрокиКорр")
				.Поле("*")
			.Ресурсы()
				.Поле("-Сумма", "Сумма")
				.Поле("-СуммаБезНДС", "СуммаБезНДС")
				.Поле("-СуммаНДС", "СуммаНДС")
			.Отбор("Корректировка")
		.База("ВТ_ТОВАРЫ")
			.Реквизиты()
				.Поле("*")
			.Ресурсы()
				.Поле("*")
			.Отбор("НЕ Корректировка")
			.Порядок("Сумма", "-")
	;
	МодельРаспределения.Распределить();
	
	Результат = Новый ТабличныйДокумент();
	ВывестиЗаголовок(Результат, Заголовок);
	ВывестиТаблицу(Результат, Объект.Товары.Выгрузить(Новый Структура("Корректировка", Ложь)), "Товары");
	ВывестиТаблицу(Результат, Объект.Товары.Выгрузить(Новый Структура("Корректировка", Истина)), "Товары корр");
	ВывестиТаблицу(Результат, МодельРаспределения.РезультатРаспределения, "Распределение корр товаров по товарам");
	ВывестиТаблицу(Результат, МодельРаспределения.БазаРаспределения, "Остатки товаров");	
	ВывестиТаблицу(Результат, МодельРаспределения.ТаблицаРаспределения, "Остатки корр товаров");
	Возврат Результат;
КонецФункции

&НаСервере
Функция РаспределитьПропорционально(Заголовок)
	МодельЗапроса = Общий.МодельЗапроса()
	;
	МодельЗапроса.ЗапросПакета().Поместить("ВТ_СУММА_РАСПРЕДЕЛЕНИЯ")
		.Поле("&СуммаРаспределения", "Сумма")
	;
	МодельЗапроса.ЗапросПакета().Поместить("ВТ_ТОВАРЫ")
		.Источник(Объект.Товары)
		.Поле("*")
	;
	МодельЗапроса.Параметр("СуммаРаспределения", Объект.СуммаРаспределения);
	МодельЗапроса.ВыполнитьЗапрос();
	
	МодельРаспределения = Общий.МодельРаспределения(МодельЗапроса.МенеджерВременныхТаблиц)
	;
	МодельРаспределения
		.Таблица("ВТ_СУММА_РАСПРЕДЕЛЕНИЯ")
			.Ресурсы()
				.Поле("Сумма")
		.База("ВТ_ТОВАРЫ")
			.Ресурсы()
				.Поле("Сумма")
				.Поле("СуммаБезНДС")
				.Поле("СуммаНДС")
			.Реквизиты()
				.Поле("*")
			.Отбор("НЕ Корректировка")
	;
	МодельРаспределения.Распределить();
	
	Результат = Новый ТабличныйДокумент();
	ВывестиЗаголовок(Результат, Заголовок);
	ВывестиТаблицу(Результат, Объект.Товары.Выгрузить(), "Товары");
	ВывестиТаблицу(Результат, МодельРаспределения.РезультатРаспределения, СтрШаблон("Распределение суммы %1 по товарам", Формат(Объект.СуммаРаспределения, "ЧЦ=15; ЧДЦ=2;")));
	ВывестиТаблицу(Результат, МодельРаспределения.БазаРаспределения, "Остатки товаров");	
	ВывестиТаблицу(Результат, МодельРаспределения.ТаблицаРаспределения, "Остатки суммы распределения");
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура Пропорция(Команда)
	Перем Заголовок;
	Заголовок = "Пропорция";
	Результат = РаспределитьПропорционально(СтрШаблон("Распределить пропорционально сумму %1", Формат(Объект.СуммаРаспределения, "ЧЦ=15; ЧДЦ=2;")));
	Результат.Показать(Заголовок);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоПартиям(Команда)
	Перем Заголовок;
	Заголовок = "Товары по партиям";
	Результат = РаспределитьТоварыПоПартиям(Заголовок);
	Результат.Показать(Заголовок);
КонецПроцедуры

&НаКлиенте
Процедура СписаниеЛимитов(Команда)
	Перем Заголовок;
	Заголовок = "Списание лимитов";
	Результат = РаспределитьСписаниеЛимитов("Списание лимитов");
	Результат.Показать(Заголовок);
КонецПроцедуры

&НаКлиенте
Процедура Зачет(Команда)
	Перем Заголовок;
	Заголовок = "Зачет";
	Результат = РаспределитьЗачет(Заголовок);
	Результат.Показать(Заголовок);
КонецПроцедуры

&НаКлиенте
Процедура Реклассификация(Команда)
	Перем Заголовок;
	Заголовок = "Реклассификация";
	Результат = РаспределитьРекласс(Заголовок);
	Результат.Показать(Заголовок);
КонецПроцедуры
